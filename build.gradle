plugins {
    id 'com.gradle.plugin-publish' version '0.9.1'
    id 'org.gradle.java'
    id 'org.gradle.eclipse'
    id 'org.gradle.groovy'
    id 'org.gradle.java-gradle-plugin'
}

group 'com.github.twostone.gradle'
version '1.0.0'

repositories {
    jcenter()
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile localGroovy()

    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
    testRuntime files(createClasspathManifest)
}

pluginBundle {
    website = 'https://github.com/TwoStone/gradle-eclipse-checkstyle'
    vcsUrl = 'https://github.com/TwoStone/gradle-eclipse-checkstyle.git'
    description = 'The Gradle Eclipse Checkstyle Plugin adds the checkstyle style nature and buildcommands to your eclipse project and generates a checkstyle configuration based on your gradle checkstyle settings.'
    tags = ['eclipse', 'checkstyle']

    plugins {
        'eclipse-checkstyle-plugin' {
            id = 'com.github.twostone.eclipse-checkstyle'
            displayName = 'Gradle Eclipse Checkstyle Plugin'
        }
    }
}
